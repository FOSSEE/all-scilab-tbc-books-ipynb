{
"cells": [
 {
		   "cell_type": "markdown",
	   "metadata": {},
	   "source": [
       "# Chapter 21: Process Monitoring"
	   ]
	},
{
		   "cell_type": "markdown",
		   "metadata": {},
		   "source": [
			"## Example 21.2: Semiconductor_processing_control_charts.sce"
		   ]
		  },
  {
"cell_type": "code",
	   "execution_count": null,
	   "metadata": {
	    "collapsed": true
	   },
	   "outputs": [],
"source": [
"clear\n",
"clc\n",
"//Example 21.2\n",
"disp('Example 21.2')\n",
"//data\n",
"x=[209.6    207.6    211.1  \n",
"    183.5    193.1    202.4  \n",
"    190.1    206.8    201.6  \n",
"    206.9    189.3    204.1  \n",
"    260.     209.     212.2  \n",
"    193.9    178.8    214.5  \n",
"    206.9    202.8    189.7  \n",
"    200.2    192.7    202.1  \n",
"    210.6    192.3    205.9  \n",
"    186.6    201.5    197.4  \n",
"    204.8    196.6    225.   \n",
"    183.7    209.7    208.6  \n",
"    185.6    198.9    191.5  \n",
"    202.9    210.1    208.1  \n",
"    198.6    195.2    150.   \n",
"    188.7    200.7    207.6  \n",
"    197.1    204.     182.9  \n",
"    194.2    211.2    215.4  \n",
"    191.     206.2    183.9  \n",
"    202.5    197.1    211.1  \n",
"    185.1    186.3    188.9  \n",
"    203.1    193.1    203.9  \n",
"    179.7    203.3    209.7  \n",
"    205.3    190.     208.2  \n",
"    203.4    202.9    200.4 ]\n",
"//Original Limits\n",
"n=3;\n",
"xbar=sum(x,2)/n; //mean calculation\n",
"s=sqrt(1/(n-1)*sum((x-repmat(xbar,1,3)).^2,2)); //standard deviation calculation\n",
"p=length(xbar);//no. of subgroups\n",
"xbarbar=sum(xbar,1)/p;\n",
"sbar=sum(s,1)/p;\n",
"c4=0.8862;B3=0;B4=2.568;c=3;\n",
"sigma=1/c4*sbar/sqrt(n);\n",
"//original limits\n",
"UCL_x=xbarbar+c*sigma;//Eqn21-9\n",
"LCL_x=xbarbar-c*sigma;//Eqn 21-10\n",
"UCL_s=B4*sbar;//Eqn21-14\n",
"LCL_s=B3*sbar;//Eqn21-15\n",
"//Modified Limits\n",
"x_mod=x;\n",
"x_mod([5 15],:)=[];\n",
"n=3;\n",
"xbar_mod=sum(x_mod,2)/n; //mean calculation\n",
"s_mod=sqrt(1/(n-1)*sum((x_mod-repmat(xbar_mod,1,3)).^2,2)); //standard deviation calculation\n",
"p_mod=length(xbar_mod);//no. of subgroups\n",
"xbarbar_mod=sum(xbar_mod,1)/p_mod;\n",
"sbar_mod=sum(s_mod,1)/p_mod;\n",
"c4=0.8862;B3=0;B4=2.568;c=3;\n",
"sigma_mod=1/c4*sbar_mod/sqrt(n);\n",
"//modified limits\n",
"UCL_x_mod=xbarbar_mod+c*sigma_mod;//Eqn21-9\n",
"LCL_x_mod=xbarbar_mod-c*sigma_mod;//Eqn 21-10\n",
"UCL_s_mod=B4*sbar_mod;//Eqn21-14\n",
"LCL_s_mod=B3*sbar_mod;//Eqn21-15\n",
"mprintf('\n                 Original Limits     Modified Limits')\n",
"mprintf('\n xbar Chart Control Limits ')\n",
"mprintf('\n UCL             %f           %f',UCL_x,UCL_x_mod)\n",
"mprintf('\n LCL             %f           %f',LCL_x,LCL_x_mod)\n",
"mprintf('\n s Chart Control Limits ')\n",
"mprintf('\n UCL             %f            %f',UCL_s,UCL_s_mod)\n",
"mprintf('\n LCL             %f             %f',LCL_s,LCL_s_mod)\n",
"subplot(2,1,1);\n",
"plot2d(repmat(UCL_x,1,p));\n",
"plot(repmat(UCL_x_mod,1,p),'--');\n",
"plot2d(repmat(LCL_x,1,p));\n",
"plot(repmat(LCL_x_mod,1,p),'--');\n",
"plot2d(xbar,style=-1,rect=[0,160,30,260])\n",
"xtitle('Example 21.2','Sampling number','$\text{Thickness}\ (\AA)$')\n",
"legend('Original Limits','Modified Limits')\n",
"subplot(2,1,2);\n",
"plot2d(repmat(UCL_s,1,p));\n",
"plot2d(repmat(LCL_s,1,p));\n",
"plot(repmat(UCL_s_mod,1,p),'--');\n",
"plot(repmat(LCL_s_mod,1,p),'--');\n",
"plot2d(s,style=-1,rect=[0,0,30,30])\n",
"xtitle('','Sampling number','$\text{Standard Deviation}\ (\AA)$')"
   ]
   }
,
{
		   "cell_type": "markdown",
		   "metadata": {},
		   "source": [
			"## Example 21.3: Shewart_CUSUM_EWMA_compariso.sce"
		   ]
		  },
  {
"cell_type": "code",
	   "execution_count": null,
	   "metadata": {
	    "collapsed": true
	   },
	   "outputs": [],
"source": [
"clear\n",
"clc\n",
"//Example 21.3\n",
"disp('Example 21.3')\n",
"T=70;sigma=3;p=100;//p is the no. of samples\n",
"x=grand(p,1, 'nor', T, sigma);\n",
"delta=0.5*sigma;\n",
"x(11:$)=x(11:$)+delta;\n",
"//Limits for Shewart charts\n",
"UCL_1=T+sigma*3;\n",
"LCL_1=T-sigma*3;\n",
"subplot(3,1,1);\n",
"plot2d(repmat(UCL_1,1,p));\n",
"plot2d(repmat(LCL_1,1,p));\n",
"plot2d(x,style=1,rect=[0,60,100,80])\n",
"xtitle('Example 21.3','Sampling number','Strength (MPa)')\n",
"//CUSUM\n",
"Cplus=zeros(100,1);Cminus=zeros(100,1);\n",
"K=0.5*sigma;H=5*sigma;\n",
"UCL_2=H;\n",
"for k=2:100\n",
"    Cplus(k)=max(0,x(k)-(T+K)+Cplus(k-1));\n",
"    Cminus(k)=max(0,(T-K)-x(k)+Cminus(k-1));\n",
"    if Cplus(k-1)>H then \n",
"        Cplus(k)=0;\n",
"    end\n",
"    if Cminus(k-1)>H then \n",
"        Cminus(k)=0;\n",
"    end\n",
"    \n",
"end\n",
"subplot(3,1,2);\n",
"plot2d(Cplus,style=1,rect=[0,0,100,20]);\n",
"plot2d(Cminus,style=2,rect=[0,0,100,20]);\n",
"plot2d(repmat(UCL_2,1,p));\n",
"xtitle('','Sampling number','CUSUM')\n",
"legend('Cplus','Cminus')\n",
"//EWMA\n",
"lamda=0.25;\n",
"z=x;\n",
"for k=2:100\n",
"    z(k)=lamda*x(k)+(1-lamda)*z(k-1);\n",
"end\n",
"UCL_3=T+3*sigma*sqrt(lamda/(2-lamda));\n",
"LCL_3=T-3*sigma*sqrt(lamda/(2-lamda));\n",
"subplot(3,1,3);\n",
"plot2d(repmat(UCL_3,1,p));\n",
"plot2d(repmat(LCL_3,1,p));\n",
"plot2d(z,style=1,rect=[0,65,100,75])\n",
"xtitle('','Sampling number','EWMA')\n",
"mprintf('The charts in the example and in the book differ due\n...\n",
"a different realization of data everytime the code is run\n...\n",
"due to the grand command. If we had the exact data as that given\n...\n",
"in the book our charts would have matched.')"
   ]
   }
,
{
		   "cell_type": "markdown",
		   "metadata": {},
		   "source": [
			"## Example 21.4: Process_Capability_Indices.sce"
		   ]
		  },
  {
"cell_type": "code",
	   "execution_count": null,
	   "metadata": {
	    "collapsed": true
	   },
	   "outputs": [],
"source": [
"clear\n",
"clc\n",
"//Example 21.4\n",
"disp('Example 21.4')\n",
"xbar=199.5;//Note that this is the correct value and not 199\n",
"sbar=8.83;\n",
"USL=235;//Note that this is diff from UCL\n",
"LSL=185;\n",
"c4=0.8862;\n",
"n=3;\n",
"sigma=5.75;\n",
"sigma_x=sbar/c4/sqrt(n);\n",
"mprintf('\nValue of sigma_x=%f',sigma_x);\n",
"Cp=(USL-LSL)/6/sigma;\n",
"Cpk=min(xbar-LSL,USL-xbar)/3/sigma;\n",
"mprintf('\nCp=%f and Cpk=%f',Cp,Cpk)"
   ]
   }
,
{
		   "cell_type": "markdown",
		   "metadata": {},
		   "source": [
			"## Example 21.5: Effluent_Stream_from_wastewater_treatment.sce"
		   ]
		  },
  {
"cell_type": "code",
	   "execution_count": null,
	   "metadata": {
	    "collapsed": true
	   },
	   "outputs": [],
"source": [
"clear\n",
"clc\n",
"//Example 21.5\n",
"disp('Example 21.5')\n",
"//data\n",
"x=[ 17.7    1380.  \n",
"    23.6    1458.  \n",
"    13.2    1322.  \n",
"    25.2    1448.  \n",
"    13.1    1334.  \n",
"    27.8    1485.  \n",
"    29.8    1503.  \n",
"    9.      1540.  \n",
"    14.3    1341.  \n",
"    26.     1448.  \n",
"    23.2    1426.  \n",
"    22.8    1417.  \n",
"    20.4    1384.  \n",
"    17.5    1380.  \n",
"    18.4    1396.  \n",
"    16.8    1345.  \n",
"    13.8    1349.  \n",
"    19.4    1398.  \n",
"    24.7    1426.  \n",
"    16.8    1361.  \n",
"    14.9    1347.  \n",
"    27.6    1476.  \n",
"    26.1    1454.  \n",
"    20.     1393.  \n",
"    22.9    1427.  \n",
"    22.4    1431.  \n",
"    19.6    1405.  \n",
"    31.5    1521.  \n",
"    19.9    1409.  \n",
"    20.3    1392.];  \n",
"T=mean(x,'r');\n",
"s=sqrt(variance(x,'r'));\n",
"UCL=T+3*s;\n",
"LCL=T-3*s;\n",
"p=size(x,1)\n",
"subplot(2,1,1);\n",
"plot2d(repmat(UCL(1),1,p));\n",
"plot2d(repmat(LCL(1),1,p));\n",
"plot2d(repmat(T(1),1,p));\n",
"plot2d(x(:,1),style=-1,rect=[0,0,32,40])\n",
"xtitle('Example 21.4','Sampling number','BOD (mg/L)')\n",
"subplot(2,1,2);\n",
"plot2d(repmat(UCL(2),1,p));\n",
"plot2d(repmat(LCL(2),1,p));\n",
"plot2d(repmat(T(2),1,p));\n",
"plot2d(x(:,2),style=-1,rect=[0,1200,32,1600])\n",
"xtitle('','Sampling number','Solids (mg/L)')\n",
"//subplot(3,1,3);\n",
"scf()\n",
"plot2d(x(8,1),x(8,2),style=-3,rect=[0,1200,40,1600])\n",
"plot2d(x(:,1),x(:,2),style=-1,rect=[0,1200,40,1600])\n",
"legend('Sample #8',position=4)\n",
"xtitle('','BOD (mg/L)','Solids (mg/L)')\n",
"mprintf('\nThe confidence interval for third case is not drawn\n...\n",
"    because it is beyond the scope of this book')"
   ]
   }
,
{
		   "cell_type": "markdown",
		   "metadata": {},
		   "source": [
			"## Example 21.6: Hotellings_T_square_statistic.sce"
		   ]
		  },
  {
"cell_type": "code",
	   "execution_count": null,
	   "metadata": {
	    "collapsed": true
	   },
	   "outputs": [],
"source": [
"clear\n",
"clc\n",
"//Example 21.6\n",
"disp('Example 21.6')\n",
"//data\n",
"x=[ 17.7    1380.  \n",
"    23.6    1458.  \n",
"    13.2    1322.  \n",
"    25.2    1448.  \n",
"    13.1    1334.  \n",
"    27.8    1485.  \n",
"    29.8    1503.  \n",
"    9.      1540.  \n",
"    14.3    1341.  \n",
"    26.     1448.  \n",
"    23.2    1426.  \n",
"    22.8    1417.  \n",
"    20.4    1384.  \n",
"    17.5    1380.  \n",
"    18.4    1396.  \n",
"    16.8    1345.  \n",
"    13.8    1349.  \n",
"    19.4    1398.  \n",
"    24.7    1426.  \n",
"    16.8    1361.  \n",
"    14.9    1347.  \n",
"    27.6    1476.  \n",
"    26.1    1454.  \n",
"    20.     1393.  \n",
"    22.9    1427.  \n",
"    22.4    1431.  \n",
"    19.6    1405.  \n",
"    31.5    1521.  \n",
"    19.9    1409.  \n",
"    20.3    1392.]; \n",
"    \n",
"    \n",
"n=1;\n",
"N=size(x,1);\n",
"T=mean(x,'r');\n",
"//For our example n=1 because each measurement is a subgroup\n",
"S=mvvacov(x);\n",
"//Note that mvvacov calculates covariance with denominator N, while\n",
"//variance caluclates with denominator N-1, hence diagonal entry of mvvacov does not\n",
"//match with variance calculated manually for each vector\n",
"//As per wikipedia the book is wrong and for covariance matrix we should\n",
"//use N-1 but here we follow the book\n",
"Tsquare=zeros(N,1);\n",
"for k=1:N\n",
"    Tsquare(k)=n*(x(k,:)-T)*inv(S)*(x(k,:)-T)';\n",
"end\n",
"UCL=11.63;\n",
"plot(repmat(UCL,1,N),color='black');\n",
"plot(Tsquare,'+')\n",
"legend('UCL 99% confidence limit')\n",
"xtitle('Example 21.6','Sample number','$T^2$')"
   ]
   }
],
"metadata": {
		  "kernelspec": {
		   "display_name": "Scilab",
		   "language": "scilab",
		   "name": "scilab"
		  },
		  "language_info": {
		   "file_extension": ".sce",
		   "help_links": [
			{
			 "text": "MetaKernel Magics",
			 "url": "https://github.com/calysto/metakernel/blob/master/metakernel/magics/README.md"
			}
		   ],
		   "mimetype": "text/x-octave",
		   "name": "scilab",
		   "version": "0.7.1"
		  }
		 },
		 "nbformat": 4,
		 "nbformat_minor": 0
}
