{
"cells": [
 {
		   "cell_type": "markdown",
	   "metadata": {},
	   "source": [
       "# Chapter 7: Optimal System Operation"
	   ]
	},
{
		   "cell_type": "markdown",
		   "metadata": {},
		   "source": [
			"## Example 7.1: Incremental_cost_and_load_sharing.sce"
		   ]
		  },
  {
"cell_type": "code",
	   "execution_count": null,
	   "metadata": {
	    "collapsed": true
	   },
	   "outputs": [],
"source": [
"//Chapter 7\n",
"//Example 7.1\n",
"//page 246\n",
"//To find incremental cost and load sharing\n",
"clear;clc;\n",
"///Let us use the program given in the Appendix G in the textbook to write\n",
"//a function that returns the value of lamda and Loading of each generator \n",
"//when the total load on the plant is sent to the function\n",
" \n",
"function [lamdaprev,Pg]=optimum(Pd)    \n",
"        n=2; //number of generators\n",
"        Alpha=[0.2 0.25];\n",
"        Beta=[40 30];\n",
"        lamda=35; //initial guess for lambda\n",
"        lamdaprev=lamda;\n",
"        eps=1; //tolerance\n",
"        deltalamda=0.25; //increment in lamda\n",
"        Pgmax=[125 125];\n",
"        Pgmin=[20 20];\n",
"        Pg=100*ones(n,1);\n",
"        while abs(sum(Pg)-Pd)>eps\n",
"            for i=1:n\n",
"                Pg(i)=(lamda-Beta(i))/Alpha(i);\n",
"                if Pg(i)>Pgmax(i) then\n",
"                    Pg(i)=Pgmax(i);\n",
"                end\n",
"                if Pg(i)<Pgmin(i) then\n",
"                    Pg(i)=Pgmin(i);\n",
"                end\n",
"           end\n",
"           if  (sum(Pg)-Pd)<0 then\n",
"               lamdaprev=lamda;\n",
"               lamda=lamda+deltalamda;\n",
"            else\n",
"               lamdaprev=lamda;\n",
"               lamda=lamda-deltalamda;   \n",
"           end\n",
"           end\n",
"endfunction\n",
"//to draw the table 7.1\n",
"printf('Table 7.1 Output of each unit and plant output for various values of lamda\n')\n",
"printf('--------------------------------------------------------------\n');\n",
"printf('Plant Lamda,        Unit 1        Unit 2        Plant Output  \n');\n",
"printf('Rs/MWh              Pg1,MW        Pg2,MW        (Pg1+Pg2),MW  \n');\n",
"printf('--------------------------------------------------------------\n');\n",
"Pd_matrix=[40 76 130 150 175 220 231.25 250];\n",
"for i=1:8\n",
"    [lamda,Pg]=optimum(Pd_matrix(i));\n",
"    printf('%0.2f               %0.2f         %0.2f          %0.2f\n',lamda,Pg(1),Pg(2),Pg(1)+Pg(2));\n",
"end\n",
"printf('--------------------------------------------------------------\n');\n",
"//To draw the Graphs 7.3 and 7.4\n",
"Pd_test=40:3.75:250;\n",
"[Pd_ro,Pd_co]=size(Pd_test)\n",
"for i=1:Pd_co\n",
"   [lamda,Pg]=optimum(Pd_test(i));\n",
"   lamda_test(i)=lamda;\n",
"   Pg1_test(i)=Pg(1);\n",
"   Pg2_test(i)=Pg(2);\n",
"end\n",
"Pg1_test=Pg1_test.'; //transposing without conjugating\n",
"Pg2_test=Pg2_test.';\n",
"lamda_test=lamda_test.';\n",
"subplot(211)\n",
"plot(Pd_test,lamda_test);\n",
"title('Incremental Fuel cost versus plant output');\n",
"xlabel('Plant output,MW');\n",
"ylabel('Incremental fuel cost,Rs/MWh');\n",
"set(gca(),'grid',[0,0])\n",
"get('current_axes');\n",
"subplot(212)\n",
"plot(Pd_test,Pg1_test,Pd_test,Pg2_test);\n",
"title('Output of each unit versus plant output');\n",
"xlabel('Plant output,MW');\n",
"ylabel('Unit output,MW');\n",
"legend(['Unit 1';'Unit 2'],[2]);\n",
"set(gca(),'grid',[0,0])\n",
"get('current_axes');"
   ]
   }
,
{
		   "cell_type": "markdown",
		   "metadata": {},
		   "source": [
			"## Example 7.2: Savings_by_optimal_scheduling.sce"
		   ]
		  },
  {
"cell_type": "code",
	   "execution_count": null,
	   "metadata": {
	    "collapsed": true
	   },
	   "outputs": [],
"source": [
"//Chapter 7\n",
"//Example 7.2\n",
"//page 248\n",
"//To find the saving in fuel cost by optimal scheduling\n",
"clear;clc;\n",
"//Example reveals that for optimal load sharing units 1&2 has to take up 50MW and 80MW respectively\n",
"//If each unit supplies 65MW,increase in cost for units 1&2 are\n",
"Increase1=integrate('0.2*Pg1+40','Pg1',50,65);\n",
"Increase2=integrate('0.25*Pg2+30','Pg2',80,65);\n",
"printf('\nIncrease in cost for unit 1 is = %0.1f Rs/hr',Increase1);\n",
"printf('\n\nIncrease in cost for unit 2 is = %0.3f Rs/hr',Increase2);\n",
"printf('\n\nNet saving caused by optimum scheduling is = %0.3f Rs/hr',Increase1+Increase2);\n",
"printf('\n\nTotal yearly saving assuming continuous operation= Rs %d',(Increase1+Increase2)*24*365);"
   ]
   }
,
{
		   "cell_type": "markdown",
		   "metadata": {},
		   "source": [
			"## Example 7.3: Economical_operation.sce"
		   ]
		  },
  {
"cell_type": "code",
	   "execution_count": null,
	   "metadata": {
	    "collapsed": true
	   },
	   "outputs": [],
"source": [
"//Chapter 7\n",
"//Example 7.3\n",
"//page 249\n",
"//To find the economical operation\n",
"clear;clc;\n",
"//from the table we got as the output in the example_7_1\n",
"//for optimum operation of load 220MW,unit 1&2 must be loaded 100MW and 120MW respwctively\n",
"//and for a load of 76MW,unit 1&2 must be loaded 20MW and 56MW respwctively\n",
"start_up=400;\n",
"//case(i)\n",
"printf('\nCase(i)');\n",
"//total fuel cost for the load of 220MW during 6AM to 6PM \n",
"Pg1=100;\n",
"Pg2=120;\n",
"C1=0.1*Pg1^2+40*Pg1+120;\n",
"C2=0.125*Pg2^2+30*Pg2+100;\n",
"total1=(C1+C2)*12;\n",
"printf('\nTotal fuel cost for the load of 220MW during 6AM to 6PM = Rs. %d',total1);\n",
"//total fuel cost for the load of 76MW during 6PM to 6AM \n",
"Pg1=20;\n",
"Pg2=56;\n",
"C1=0.1*Pg1^2+40*Pg1+120;\n",
"C2=0.125*Pg2^2+30*Pg2+100;\n",
"total2=(C1+C2)*12;\n",
"printf('\nTotal fuel cost for the load of 76MW during 6PM to 6AM if both the units run = Rs. %d',total2);\n",
"total=total1+total2; //total fuel cost for 24hrs\n",
"printf('\nTotal fuel cost for the load during 24hrs if both the units run = Rs. %d',total);\n",
"//case(ii)\n",
"printf('\n\nCase(ii)');\n",
"//If during light load condition unit2 is On and Unit1 is Off then\n",
"Pg2=76;\n",
"C2=0.125*Pg2^2+30*Pg2+100;\n",
"total2=C2*12;\n",
"total_case2=total1+total2+start_up;\n",
"printf('\nTotal fuel cost for the 24hrs laod if only unit 2 run during light loads is = Rs. %d',total_case2);"
   ]
   }
,
{
		   "cell_type": "markdown",
		   "metadata": {},
		   "source": [
			"## Example 7.4: Generation_and_losses_incurred.sce"
		   ]
		  },
  {
"cell_type": "code",
	   "execution_count": null,
	   "metadata": {
	    "collapsed": true
	   },
	   "outputs": [],
"source": [
"//Chapter 7\n",
"//Example 7.4\n",
"//page 263\n",
"//To find required generation for each plant and losses incurred\n",
"clear;clc;\n",
"///Let us use the program given in the Appendix G in the textbook which includes penalty factor also to write\n",
"//a function that returns the value of lamda,Loading of each generator and losses\n",
"//when the total load on the plant is sent to the function\n",
"function [lamda,Pg,PL]=optimum2(Pd)\n",
"n=2; //no of generators\n",
"Alpha=[0.02 0.04];\n",
"Beta=[16 20];\n",
"lamda=20; //initial value of lamda\n",
"lamdaprev=lamda;\n",
"eps=1; //tolerance\n",
"deltalamda=0.1;\n",
"Pgmax=[200 200];\n",
"Pgmin=[0 0];\n",
"B=[0.001 0;0 0];\n",
"Pg=zeros(n,1);\n",
"noofiter=0;\n",
"PL=0;\n",
"Pg=zeros(n,1);\n",
"while abs(sum(Pg)-Pd-PL)>eps\n",
"    for i=1:n\n",
"        sigma=B(i,:)*Pg-B(i,i)*Pg(i);\n",
"        Pg(i)=(1-(Beta(i)/lamda)-(2*sigma))/(Alpha(i)/lamda+2*B(i,i));\n",
"        PL=Pg.'*B*Pg;\n",
"        if Pg(i)>Pgmax(i) then\n",
"            Pg(i)=Pgmax(i);\n",
"        end\n",
"        if Pg(i)<Pgmin(i) then\n",
"            Pg(i)=Pgmin(i);\n",
"        end\n",
"    end\n",
"    PL=Pg.'*B*Pg;\n",
"    if(sum(Pg)-Pd-PL)<0 then\n",
"        lamdaprev=lamda;\n",
"        lamda=lamda+deltalamda;\n",
"    else\n",
"        lamdaprev=lamda;\n",
"        lamda=lamda-deltalamda;\n",
"    end\n",
"    noofiter=noofiter+1;\n",
"    Pg;\n",
"end\n",
"endfunction\n",
"//In this example let us take the answer .i.e load(Pd)=237.04MW and calculate \n",
"//lamda so that we can use the algorithm used in the textbook\n",
"Pd=237.04\n",
"[lamda_test,Pg_test,PL_test]=optimum2(Pd);\n",
"printf('\nLagrange''s multiplier (lamda) is\n Lamda =%0.1f',lamda_test);\n",
"printf('\n\nRequired generation for optimum loading are \n Pg1=%0.2f MW \n Pg2=%d MW\n',Pg_test(1),Pg_test(2));\n",
"printf('\nThe transmission power loss is\n PL=%0.2f MW',PL_test);\n",
"printf('\n\nThe load is \n Pd=%0.2f MW',Pd);"
   ]
   }
,
{
		   "cell_type": "markdown",
		   "metadata": {},
		   "source": [
			"## Example 7.5: Savings_on_coordination_of_losses.sce"
		   ]
		  },
  {
"cell_type": "code",
	   "execution_count": null,
	   "metadata": {
	    "collapsed": true
	   },
	   "outputs": [],
"source": [
"//Chapter 7\n",
"//Example 7.5\n",
"//page 264\n",
"//To find savings when losses are coordinated\n",
"clear;clc;\n",
"function [lamdaprev,Pg]=optimum(Pd)    \n",
"        n=2; //number of generators\n",
"        Alpha=[0.02 0.04];\n",
"        Beta=[16 20];\n",
"        lamda=20; //initial guess for lambda\n",
"        lamdaprev=lamda;\n",
"        eps=1; //tolerance\n",
"        deltalamda=0.25; //increment in lamda\n",
"        Pgmax=[200 200];\n",
"        Pgmin=[0 0];\n",
"        Pg=100*ones(n,1);\n",
"        while abs(sum(Pg)-Pd)>eps\n",
"            for i=1:n\n",
"                Pg(i)=(lamda-Beta(i))/Alpha(i);\n",
"                if Pg(i)>Pgmax(i) then\n",
"                    Pg(i)=Pgmax(i);\n",
"                end\n",
"                if Pg(i)<Pgmin(i) then\n",
"                    Pg(i)=Pgmin(i);\n",
"                end\n",
"           end\n",
"           if  (sum(Pg)-Pd)<0 then\n",
"               lamdaprev=lamda;\n",
"               lamda=lamda+deltalamda;\n",
"            else\n",
"               lamdaprev=lamda;\n",
"               lamda=lamda-deltalamda;   \n",
"           end\n",
"           end\n",
"endfunction\n",
"//the above function 'optimum' doesn't coordinate losses\n",
"//case(i) when the losses are included but not coordinated\n",
"[lamda_case1,Pg_case1]=optimum(237.04);\n",
"//since Pg2 does not supply transmission losses and the losses are supplied only by Pg1\n",
"Pg2_1=Pg_case1(2);\n",
"//to get Pg1 we will solve Pg1+Pg2=0.001*Pg1^2+237.04\n",
"//the above equation can be wriiten as (0.001*Pg1^2) - Pg1 +(237.04-Pg2) =0\n",
"p=poly([0.001 -1 (237.04+Pg2_1)],'Pg1');\n",
"Pg1_1=roots(p);\n",
"Pg1_1=Pg1_1(1);\n",
"printf('\ncase(i) when the losses are included but not coordinated');\n",
"printf('\nPg1=%0.2f MW    Pg2=%0.2f MW',Pg1_1,Pg2_1);\n",
"//case(ii) when the losses are also coordinated\n",
"//we have the solution for case(ii) from example_7_4\n",
"Pg1_2=128.57; Pg2_2=125; //case(ii)\n",
"printf('\n\ncase(ii) when the losses are coordinated');\n",
"printf('\nPg1=%0.2f MW    Pg2=%0.2f MW',Pg1_2,Pg2_2);\n",
"//saving at plant 1 is\n",
"saving1=integrate('0.02*Pg1+16','Pg1',Pg1_2,Pg1_1);\n",
"printf('\n\nSaving at plant 1 due to loss coordination is = Rs %0.2f/hr',saving1);\n",
"//saving at plant 2 is\n",
"saving2=integrate('0.04*Pg2+20','Pg2',Pg2_2,Pg2_1);\n",
"printf('\n\nSaving at plant 2 due to loss coordination is = Rs %0.2f/hr',saving2);\n",
"//net savings achieved\n",
"printf('\n\nThe net saving achieved by coordinating losses while scheduling the recieved load of 237.04MW is Rs %0.2f/hr',saving1+saving2);"
   ]
   }
,
{
		   "cell_type": "markdown",
		   "metadata": {},
		   "source": [
			"## Example 7.6: Loss_formula_coefficients_calculation.sce"
		   ]
		  },
  {
"cell_type": "code",
	   "execution_count": null,
	   "metadata": {
	    "collapsed": true
	   },
	   "outputs": [],
"source": [
"//Chapter 7\n",
"//Example 7.6\n",
"//page 268\n",
"//To calculate the loss formula coefficients of the system \n",
"clear;clc;\n",
"Ia=2-%i*0.5;    Ic=1-%i*0.25;\n",
"Ib=1.6-%i*0.4;    Id=3.6-%i*0.9;\n",
"Za=0.015+%i*0.06;    Zc=0.01+%i*0.04;\n",
"Zb=0.015+%i*0.06;    Zd=0.01+%i*0.04;\n",
"ID=Id+Ic ;//total load current\n",
"//calculation of current distribution factors\n",
"printf('\nCurrent distribution factors are :\n')\n",
"Ma1=(ID/ID);\n",
"Ma2=(0/ID);\n",
"Mb1=(-Ic/ID);\n",
"Mb2=(Id/ID);\n",
"Mc1=(Ic/ID);\n",
"Mc2=(Ic/ID);\n",
"Md1=(Id/ID);\n",
"Md2=(Id/ID);\n",
"printf('Ma1=%d\tMb1=%0.4f\tMc1=%0.4f\tMd1=%0.4f\nMa2=%d\tMb2=%0.4f\tMc2=%0.4f\tMd2=%0.4f',Ma1,Mb1,Mc1,Md1,Ma2,Mb2,Mc2,Md2);\n",
"//bus voltage calcultion\n",
"[V1_mag,V1_ang]=polar(1.0+Ia*Za);\n",
"[V2_mag,V2_ang]=polar(1+Ib*Zb);\n",
"V1_ang=real(V1_ang)*180/%pi;\n",
"V2_ang=real(V2_ang)*180/%pi;\n",
"printf('\n\nBus voltages are given by \nV1=%0.3f @ %0.2fdeg PU\tV2=%0.3f @ %0.2fdeg PU',V1_mag,V1_ang,V2_mag,V2_ang);\n",
"//current phase angles at the plants\n",
"sigma1=atand(imag(Ia)/real(Ia));\n",
"sigma2=atand(imag(Ib+Ic)/real(Ib+Ic));\n",
"printf('\n\nCurrent phase angles at the plants\nSigma1=%ddeg\tSigma2=%ddeg',sigma1,sigma2);\n",
"//plant power factors\n",
"pf1=cosd(V1_ang-sigma1);\n",
"pf2=cosd(V2_ang-sigma2);\n",
"printf('\n\nThe plant power factors are\npf1=%0.4f\tpf2=%0.4f',pf1,pf2);\n",
"//calculation of loss coefficients\n",
"B11=(Ma1*Ma1*real(Za)+Mb1*Mb1*real(Zb)+Mc1*Mc1*real(Zc)+Md1*Md1*real(Zd))/(V1_mag*V1_mag*pf1*pf1);\n",
"B22=(Ma2*Ma2*real(Za)+Mb2*Mb2*real(Zb)+Mc2*Mc2*real(Zc)+Md2*Md2*real(Zd))/(V2_mag*V2_mag*pf2*pf2);\n",
"B12=(Ma1*Ma2*real(Za)+Mb1*Mb2*real(Zb)+Mc1*Mc2*real(Zc)+Md1*Md2*real(Zd))/(V1_mag*V2_mag*pf1*pf2);\n",
"printf('\n\nThe Loss coefficients in PU are \nB11=%0.5f pu\nB22=%0.5f pu\nB12=%0.5f pu',B11,B22,B12);\n",
"printf('\n\nThe Loss coefficients in reciprocal megawatts are \nB11=%0.8f MW^-1\nB22=%0.8f MW^-1\nB12=%0.8f MW^-1',B11/100,B22/100,B12/100);"
   ]
   }
,
{
		   "cell_type": "markdown",
		   "metadata": {},
		   "source": [
			"## Example 7.7: Optimal_generation_schedule_for_hydrothermal_system.sce"
		   ]
		  },
  {
"cell_type": "code",
	   "execution_count": null,
	   "metadata": {
	    "collapsed": true
	   },
	   "outputs": [],
"source": [
"//Chapter 7\n",
"//Example 7.7\n",
"//page 281\n",
"//To find the optimal generation schedule for a typical day of the fundamental hydrothermal system\n",
"clear;clc;\n",
"h_b=20;//basic head of the water\n",
"e=0.005; //head correction factor\n",
"r=2; //non-effective water discharge\n",
"Pd_1=7; Pd_2=10; Pd_3=5; //load at three intervals of time during a day\n",
"alpha=0.5;//positive scalar\n",
"X_0=100; //initial water storage in the reservoir\n",
"X_3=60; //final water storage in the reservoir\n",
"//let us assume the initial values of the control variables\n",
"q_2=15;\n",
"q_3=15;\n",
"i=0; //iteration count\n",
"grad_2=1;grad_3=1; //inital value for iterations\n",
"while ((grad_2>0.1)|(grad_3>0.1))\n",
"//water discharge in the first interval\n",
"q_1=X_0-X_3-(q_2+q_3);\n",
"//water level after the first intervals are\n",
"X_1=X_0-q_1;\n",
"X_2=X_1-q_2;\n",
"//hydro generations in the subintervals \n",
"Pgh_1=9.81*(10^-3)*20*(1+0.5*e*(X_1+X_0))*(q_1-r);\n",
"Pgh_2=9.81*(10^-3)*20*(1+0.5*e*(X_2+X_1))*(q_2-r);\n",
"Pgh_3=9.81*(10^-3)*20*(1+0.5*e*(X_3+X_2))*(q_3-r);\n",
"//thermal generation in the three intervals\n",
"Pgt_1=Pd_1-Pgh_1;\n",
"Pgt_2=Pd_2-Pgh_2;\n",
"Pgt_3=Pd_3-Pgh_3;\n",
"//calculating lamda_1 for three subintervals\n",
"lamda_1_1=Pgt_1+25;\n",
"lamda_1_2=Pgt_2+25;\n",
"lamda_1_3=Pgt_3+25;\n",
"//since we are considering lossless case\n",
"lamda_3_1=lamda_1_1;\n",
"lamda_3_2=lamda_1_2;\n",
"lamda_3_3=lamda_1_3;\n",
"//for calculating lamda_2 for three intervals\n",
"lamda_2_1=lamda_3_1*9.81*(10^-3)*20*(1+0.5*e*(2*X_0-2*q_1+r));\n",
"lamda_2_2=lamda_2_1-lamda_3_1*(0.5*9.81*(10^-3)*20*e*(q_1-r))-lamda_3_2*(0.5*9.81*(10^-3)*20*e*(q_2-r));\n",
"lamda_2_3=lamda_2_2-lamda_3_2*(0.5*9.81*(10^-3)*20*e*(q_2-r))-lamda_3_3*(0.5*9.81*(10^-3)*20*e*(q_3-r));\n",
"//calculation of gradient vector\n",
"grad_2=lamda_2_2-lamda_3_2*9.81*(10^-3)*20*(1+0.5*e*(2*X_1-2*q_2+r));\n",
"grad_3=lamda_2_3-lamda_3_3*9.81*(10^-3)*20*(1+0.5*e*(2*X_2-2*q_3+r));\n",
"q_2=q_2-alpha*grad_2; //updating value of q and reiterating\n",
"q_3=q_3-alpha*grad_3;\n",
"i=i+1;\n",
"end\n",
"//Hydel and thermal generation for the three sub interavals are given in tabular format\n",
"printf('\nResults for Optimal Loading of Hydrothermal stations at the end of %d iterations',i);\n",
"printf('\n---------------------------------------------------------------------------------------\n');\n",
"printf('Interval\t\tLoad\t\tHydro\t\tThermal\t\tWater discharge\n');\n",
"printf('        \t\tMW\t\tMW\t\tMW\t\tm^3/s\n');\n",
"printf('---------------------------------------------------------------------------------------\n');\n",
"printf('   1    \t\t%d\t\t%0.4f\t\t%0.4f\t\t%0.2f\n',Pd_1,Pgh_1,Pgt_1,q_1);\n",
"printf('   2    \t\t%d\t\t%0.4f\t\t%0.4f\t\t%0.2f\n',Pd_2,Pgh_2,Pgt_2,q_2);\n",
"printf('   3    \t\t%d\t\t%0.4f\t\t%0.4f\t\t%0.2f\n',Pd_3,Pgh_3,Pgt_3,q_3);\n",
"printf('---------------------------------------------------------------------------------------\n');"
   ]
   }
],
"metadata": {
		  "kernelspec": {
		   "display_name": "Scilab",
		   "language": "scilab",
		   "name": "scilab"
		  },
		  "language_info": {
		   "file_extension": ".sce",
		   "help_links": [
			{
			 "text": "MetaKernel Magics",
			 "url": "https://github.com/calysto/metakernel/blob/master/metakernel/magics/README.md"
			}
		   ],
		   "mimetype": "text/x-octave",
		   "name": "scilab",
		   "version": "0.7.1"
		  }
		 },
		 "nbformat": 4,
		 "nbformat_minor": 0
}
